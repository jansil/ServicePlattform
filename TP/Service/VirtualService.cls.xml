<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="TP.Service.VirtualService">
<ProcedureBlock>1</ProcedureBlock>
<Super>EnsLib.SOAP.GenericService</Super>
<TimeCreated>62341,81962.678718</TimeCreated>

<Parameter name="SERVICENAME">
<Default>TP.Service.VirtualService</Default>
</Parameter>

<Parameter name="ADAPTER">
<Default>EnsLib.SOAP.InboundAdapter</Default>
</Parameter>

<UDLText name="T">
<Content><![CDATA[
// Parameter ADAPTER;

]]></Content>
</UDLText>

<Parameter name="SETTINGS">
<Default>Validation</Default>
</Parameter>

<Method name="OnProcessInput">
<FormalSpec><![CDATA[pRequestBody:%CharacterStream,pResponseBody:%CharacterStream,&pAction:%String]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim tSC As %Status
	#Dim tException As %Exception.SystemException
	
	Set tSC = $$$OK
	
	Try {
	 	#Dim tResponse As TP.Message.SOAPResponse
		
		// Handling for chuncked
		Set %SplitBody = 1
		
		Set pResponseBody=$$$NULLOREF
		Set tRequest=##class(TP.Message.SOAPRequest).%New(pRequestBody,,"%iaHeader",pRequestBody.GetAttribute("EnvelopeStream"),,##class(EnsLib.EDI.XML.Document).%New(..ImportHandler))
		Set tSC=..resolveAndIndex(tRequest)
		If ($$$ISERR(tSC)) { Quit }
		
		Set tUrl = tRequest.HTTPHeaders.GetAt("URL")		
		
		//Get SOAP Header
		#Dim txPath As %XML.XPATH.Document
		#Dim txPathRes As %XML.XPATH.ValueResult
		#Dim tList As %ListOfObjects
		#Dim tSoapHeaderTo As %String
		#Dim tSoapHeaderFrom As %String
		#Dim tWsaTo As %String
		#Dim tWsaFrom As %String
		#Dim tLogicalAddress As %String
		
		Set tSoapHeaderTo = ""
		Set tSoapHeaderFrom = ""
		Set tWsaTo = ""
		Set tWsaFrom = ""
		Set tRequest.SOAPAction = ""
		Set tLogicalAddress = ""
		
		//Get SOAP Action from HTTP header
		If (tRequest.HTTPHeaders.GetAt("soapaction") '= "") {
			Set tRequest.SOAPAction = $REPLACE(tRequest.HTTPHeaders.GetAt("soapaction"), $Char(34), "")
			$$$TRACE("Value from HTTP Header soapaction: " _ tRequest.SOAPAction)
		}
		
		Set tSC = ##class(%XML.XPATH.Document).CreateFromStream(tRequest.EnvelopeStream, .txPath)
		Set txPath.PrefixMappings = "soapenv http://schemas.xmlsoap.org/soap/envelope/,add http://www.w3.org/2005/08/addressing"
		
		//Get SOAP Header To
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/add:To", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tSoapHeaderTo = txPathRes.Value
			
			$$$TRACE("Value from SOAP Header To: " _ tSoapHeaderTo)
		}
		
		//Get SOAP Header From
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/add:From", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tSoapHeaderFrom = txPathRes.Value
			
			$$$TRACE("Value from SOAP Header From " _ tSoapHeaderFrom)
		}
		
		//Get LogicalAddress from SOAP header
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/*[local-name()='LogicalAddress']", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tLogicalAddress = txPathRes.Value
			
			$$$TRACE("Value from SOAP Header LogicalAddress: " _ tLogicalAddress)
		}
		
		//Get prefix and namespace for root namespace from Body
		#Dim txPathBody As %XML.XPATH.Document
		#Dim txPathBodyRes As %XML.XPATH.ValueResult
		#Dim tListBody As %ListOfObjects
		#Dim tBodyPrefix As %String
		#Dim tBodyNamespace As %String
		
		Set tSC = ##class(%XML.XPATH.Document).CreateFromStream(tRequest.Stream, .txPathBody)
		Set txPathBody.PrefixMappings = "soapenv http://schemas.xmlsoap.org/soap/envelope/"
		
		//Get prefix
		Set tBodyPrefix = ""
		Set tSC = txPathBody.EvaluateExpression("/soapenv:Body/*", "name()", .tListBody)
		If (tListBody.Count() > 0) {
			Set txPathBodyRes = tListBody.GetAt(1)
			If ($FIND(txPathBodyRes, ":") > 0) {
				Set tBodyPrefix = $PIECE(txPathBodyRes.Value, ":", 1)
			}
			
			$$$TRACE("Value from SOAP Body prefix " _ tBodyPrefix)
		}
		
		//Get namespace
		If (tBodyPrefix '= "") {
			Set tSC = txPathBody.EvaluateExpression("/soapenv:Body/*", "namespace::" _ tBodyPrefix, .tListBody)
		}
		Else {
			Set tSC = txPathBody.EvaluateExpression("/soapenv:Body/*", "namespace-uri()", .tListBody)
		}
		If (tListBody.Count() > 0) {
			Set txPathBodyRes = tListBody.GetAt(1)
			Set tBodyNamespace = txPathBodyRes.Value
			Set tRequest.SOAPNamespace = txPathBodyRes.Value
			
			$$$TRACE("Value from SOAP Body root namespace " _ tRequest.SOAPNamespace)
		}
		
		/*************
		IIS
		**************/
		
		//Get original url from ARR x-original-url HTTP header
		//This header is used to create the correct SOAP namespace
		#Dim tOriginalURL As %String
		#Dim tRivVersion As %String
		Set tOriginalURL = ""
		Set tRivVersion = ""
		If (tRequest.HTTPHeaders.GetAt("x-original-url") '= "") {
			//IIS, Apache
			Set tOriginalURL = $REPLACE(tRequest.HTTPHeaders.GetAt("x-original-url"), $Char(34), "")
			
		        //Set tRequest.SOAPNamespace = $REPLACE($REPLACE(tOriginalURL,"/csp/","urn:riv:crm:"),"/",":")
		        //$$$TRACE("Value from HTTP Header x-original-url: " _ tRequest.SOAPNamespace)
		}
		$$$TRACE("OrigURL: " _ tOriginalURL)
		Set tRequest.OriginalURL = tOriginalURL
		If (tOriginalURL '= "") {
			Set list = $LISTFROMSTRING(tOriginalURL, "/")
			For i=1:1:$LISTLENGTH(list) {
				If ($ZCONVERT($EXTRACT($LIST(list, i), 1, 3), "L") = "riv") {
					Set tRivVersion = $LIST(list, i)
					Set tRequest.SOAPNamespace = tRequest.SOAPNamespace _ ":" _ tRivVersion
				}
			}
		}
		
		//Get WS-Addressing To SOAP Header
		Set txPath.PrefixMappings = "wsa http://www.w3.org/2005/08/addressing"
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/wsa:To", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tWsaTo = txPathRes.Value
			Set tRequest.WSATo = tWsaTo
			
			$$$TRACE("Value from SOAP Header WSA-To: " _ tWsaTo)
		}
		
		//Get WS-Addressing From SOAP Header
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/wsa:From/wsa:Address", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tWsaFrom = txPathRes.Value
			Set tRequest.WSAFrom = tWsaFrom
			
			$$$TRACE("Value from SOAP Header WSA-From: " _ tWsaFrom)
		}
		Else {
			Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/wsa:From", "text()", .tList)
			If (tList.Count() > 0) {
				Set txPathRes = tList.GetAt(1)
				Set tWsaFrom = txPathRes.Value
				Set tRequest.WSAFrom = tWsaFrom
			
				$$$TRACE("Value from SOAP Header WSA-From: " _ tWsaFrom)
			}
		}
		
		//Get WS-Addressing SOAP action
		Set tSC = txPath.EvaluateExpression("/soapenv:Envelope/soapenv:Header/wsa:Action", "text()", .tList)
		If (tList.Count() > 0) {
			Set txPathRes = tList.GetAt(1)
			Set tRequest.SOAPAction = txPathRes.Value
			
			$$$TRACE("Value from SOAP Header WSA-Action: " _ tRequest.SOAPAction)
		}
		
		//If WSA-To is empty but we have value in LogicalAddress, use it
		If ((tWsaTo = "") && (tLogicalAddress '= "")) { Set tWsaTo = tLogicalAddress }
		
		//If WSA-To existed, use that for routing, otherwise old SOAP Header To field
		If (tWsaTo '= "") {
			Set tRequest.ToSystem = tWsaTo
		}
		Else {
			Set tRequest.ToSystem = tSoapHeaderTo
		}
		
		If (tRequest.ToSystem	= "") {
			// Create error VP003
			Throw ##class(TP.Common.Exception).%New("ReceiverIdMissing","VP003","TP.Service.VirtualService","ReceiverId missing.")
		}
		
		//Get certificate from HTTP header
		#Dim tCommonName As %String
		#Dim tRawCertificate As %String
		Set tRawCertificate = ""
		Set tCommonName = ""
		If (tRequest.HTTPHeaders.GetAt("x-arr-clientcert") '= "") {
			//IIS
			Set tRawCertificate = $REPLACE(tRequest.HTTPHeaders.GetAt("x-arr-clientcert"), $Char(34), "")
		}
		ElseIf (tRequest.HTTPHeaders.GetAt("x-vp-auth-cert") '= "") {
			//Apache
			Set tRawCertificate = $REPLACE(tRequest.HTTPHeaders.GetAt("x-vp-auth-cert"), $Char(34), "")
			Set pos = $FIND(tRawCertificate, "-----BEGIN CERTIFICATE----- ")
			Set tRawCertificate = $EXTRACT(tRawCertificate, pos, *)
			Set pos = $FIND(tRawCertificate, " -----END")
			Set tRawCertificate = $EXTRACT(tRawCertificate, 1, pos-10)
		}
		$$$TRACE("tRawCertificate=" _ tRawCertificate)
		
		If (tRawCertificate '= "") {
			Set tCommonName = ##class(TP.Common.CertificateHelper).GetSerialNumber(tRawCertificate)
			$$$TRACE("Value from HTTP Header Common Name: " _ tCommonName)
			
			If (tCommonName = "") {
				// Create error VP002
				Throw ##class(TP.Common.Exception).%New("SerialNoMissing","VP002","TP.Service.VirtualService","SerialNumber missing in client certificate.")
			}
		}
		
		If (tRequest.HTTPHeaders.GetAt("x-rivta-original-serviceconsumer-hsaid") '= "") {
			//From National Service Platform, overwrite Common Name
			Set tCommonName = tRequest.HTTPHeaders.GetAt("x-rivta-original-serviceconsumer-hsaid")
		}
		
		//If WSA-From existed, use that for routing, otherwise old SOAP Header From field
		If (tCommonName '= "") {
			Set tRequest.FromSystem = tCommonName
		}
		ElseIf (tWsaFrom '= "") {
			Set tRequest.FromSystem = tWsaFrom
		}
		Else {
			Set tRequest.FromSystem = tSoapHeaderFrom
		}
		
		If (..OneWay) {
			Set tSC = ..SendRequestAsync(..TargetConfigName,tRequest)
			Quit
		}
		
		Set tSC=..SendRequestSync(..TargetConfigName,tRequest,.tResponse)
		$$$TRACE("SendRequestSync error: " _ tSC)
		$$$TRACE("Response.Status: " _ tResponse.Status)
		If ($$$ISERR(tSC) || $$$ISERR(tResponse.Status)) {
				If ($$$ISERR(tSC)) {
					Do $system.Status.DecomposeStatus(tSC, .err)
				}
				Else {
					Do $system.Status.DecomposeStatus(tResponse.Status, .err)
				}
				set s = ""
				For i=1:1:$Get(err) { set s = s _ "Error(" _ i _ ") = " _ err(i) _ " / " }
			
				If (..SoapVersion = "1.2") {
					Set tSoapFault = ##class(%SOAP.Fault12).%New()
					Set tSoapFault.faultcode = $$$FAULTSender
					Set tSoapFault.faultstring = "Exception"
					Set tSoapFault.detail = err(1)
				
					//Do ..ReturnFault(tSoapFault)
				}
				Else {
					Set tSoapFault = ##class(%SOAP.Fault).%New()
					Set tSoapFault.faultcode = $$$FAULTClient
					Set tSoapFault.faultstring = "Exception"
					Set tSoapFault.detail = err(1)
					
					//Do ..ReturnFault(tSoapFault)
				}
			
				Quit
			
		}
		
		#; Adapter will reassemble response stream from message envelope/header and body streams; here we prepare the streams
		If $IsObject(tResponse.EnvelopeDoc) && ('$IsObject(tResponse.EnvelopeStream) || 'tResponse.EnvelopeStream.Size) {
			Set tEnvelopeStream=##class(%GlobalBinaryStream).%New()
			Set tSC=tResponse.EnvelopeDoc.OutputToLibraryStream(tEnvelopeStream,..%VDocFormat)
			Do:$$$ISERR(tSC) ..ReturnMethodStatusFault(tSC)
		} Else {
			Set tEnvelopeStream=tResponse.EnvelopeStream
		}
		If $IsObject(tResponse.Doc) && ('$IsObject(tResponse.Stream) || 'tResponse.Stream.Size) {
			Set pResponseBody=##class(%GlobalCharacterStream).%New()
			Set tSC=tResponse.EnvelopeDoc.OutputToLibraryStream(pResponseBody,..%VDocFormat)
			Do:$$$ISERR(tSC) ..ReturnMethodStatusFault(tSC)
		} Else {
			Set pResponseBody=tResponse.Stream
			Set:'$IsObject(pResponseBody) pResponseBody=##class(%GlobalCharacterStream).%New()
		}
		If ""'=tEnvelopeStream { Set pResponseBody.Attributes("EnvelopeStream")=tEnvelopeStream }
		Else { Set tEnvelopeStream=pResponseBody } ; just to attach headers to

		#; Pass along Status line and selected response HTTP headers
		Set tSL=""
		Set tDoNotPassThrough=","_$ZCVT(..%ExcludeResponseHttpHeaders,"L")_",content-length,"
		Set tHeaderKey=""
		For { 
			Set tHeaderKey=tResponse.HTTPHeaders.Next(tHeaderKey)  Quit:""=tHeaderKey  Set tHeaderLwr=$ZCVT(tHeaderKey,"L")
			Set:tDoNotPassThrough'[(","_tHeaderLwr_",") tEnvelopeStream.Attributes("HttpHeaders",tHeaderKey)=tResponse.HTTPHeaders.GetAt(tHeaderKey) ; no need to handle multiple on one line
			Set:tHeaderLwr="statusline" tSL=tResponse.HTTPHeaders.GetAt(tHeaderKey)
		}
		Set:tDoNotPassThrough'["statusline" tEnvelopeStream.Attributes("StatusLine")=tSL
	}
	Catch tException {
		Set tSC = tException.AsStatus()
	}
	
	//Create SOAP fault
	If $$$ISERR(tSC) {
		Set pResponseBody = ..OnErrorStream(tSC, $this, "1.0")
		Set pResponseBody.Attributes("StatusLine")="HTTP/1.1 500"
	}
	
	Quit tSC
]]></Implementation>
</Method>

<Method name="OnErrorStream">
<Description>
Control the type and content of error returned to the SOAP caller</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>pStatus:%Status,pInstance:EnsLib.SOAP.Service,pSOAPVer:%String="1.0"</FormalSpec>
<Implementation><![CDATA[
	Set tStream = ##class(%GlobalBinaryStream).%New() $$$ASSERT($IsObject(tStream))
    
	Do $system.Status.DecomposeStatus(pStatus, .err)
	Set pos = $FIND(err(1), "ERROR #")
	Set tFaultCode = $EXTRACT(err(1), pos, pos+4)
	
	If tFaultCode = "VP001"
	{
		Set tFaultString = "Riv-version inte konfigurerad för den anslutningspunkt som den virtualiserade tjänsten publicerar."
	}
	ElseIf tFaultCode = "VP002"
	{
		Set tFaultString = "SERIALNUMBER ej tillgängligt i konsumentens certifikat i namn-attributet."
	}
	ElseIf tFaultCode = "VP003"
	{
		Set tFaultString = "ReceiverId ej ifylld i RivHeadern i inkommande meddelande."
	}
	ElseIf tFaultCode = "VP004"
	{
		Set tFaultString = "Det finns ingen tjänsteproducent definierad i Tjänstekatalogen som matchar ReceiverId, Tjänstekontrakt och dagens datum."
	}
	ElseIf tFaultCode = "VP005"
	{
		Set tFaultString = "Det finns ingen tjänsteproducent definierad i Tjänstekatalogen som matchar Riv-version, konvertering mellan rivversioner inte implementerat."
	}
	ElseIf tFaultCode = "VP006"
	{
		Set tFaultString = "Det finns mer än 1 tjänsteproducent definierad i Tjänstekatalogen som matchar ReceiverId, Tjänstekontrakt och dagens datum. Tyder på att Tjänstekatalogen är felkonfigurerad."
	}
	ElseIf tFaultCode = "VP007"
	{
		Set tFaultString = "Den finns ingen behörighet för den tjänstekonsument som anropar att samverka med tjänsteproducenten. Avser behörigheter definierade i Tjänstekatalogen."
	}
	ElseIf tFaultCode = "VP008"
	{
		Set tFaultString = "Ingen kontakt med Tjänstekatalogen."
	}
	ElseIf tFaultCode = "VP009"
	{
		Set tFaultString = "Fel vid kontakt med tjänsteproducenten."
	}
	ElseIf tFaultCode = "VP010"
	{
		Set tFaultString = "Ingen adress angiven i tjänsteproducenten i Tjänstekatalogen"
	}
	ElseIf tFaultCode = "VP999"
	{
		Set tFaultString = "Tekniskt fel"
	}
	Else
	{
		Set tFaultString = "Okänt fel"
	}
	                           
	Set tSoapFault = "<?xml version='1.0' encoding='UTF-8' standalone='no' ?>" _ $Char(13) _
	" <SOAP-ENV:Envelope xmlns:SOAP-ENV='http://schemas.xmlsoap.org/soap/envelope/' xmlns:xsi='http://www.w3.org/2001/XMLSchema-instance' xmlns:s='http://www.w3.org/2001/XMLSchema' >" _ $Char(13) _
	" <SOAP-ENV:Body>" _ $Char(13) _
	" <SOAP-ENV:Fault>" _ $Char(13) _
	" <faultcode>" _ tFaultCode _  "</faultcode>" _  $Char(13) _
	" <faultstring>" _ tFaultString _  "</faultstring>" _  $Char(13) _
	//" <detail></detail>" _ $Char(13) _
	" </SOAP-ENV:Fault>" _ $Char(13) _
	" </SOAP-ENV:Body>" _ $Char(13) _
	" </SOAP-ENV:Envelope>"

	Do tStream.Write(tSoapFault)
	Quit tStream
]]></Implementation>
</Method>
</Class>
</Export>
