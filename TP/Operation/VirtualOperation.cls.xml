<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="TP.Operation.VirtualOperation">
<Super>EnsLib.SOAP.GenericOperation</Super>
<TimeCreated>62922,57924.338597</TimeCreated>

<Parameter name="ADAPTER">
<Default>EnsLib.HTTP.OutboundAdapter</Default>
</Parameter>

<Method name="OnMessage">
<Description>
Invoke a remote SOAP Service given a generic SOAP request</Description>
<FormalSpec>pRequest:TP.Message.SOAPRequest,*pResponse:TP.Message.SOAPResponse</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#Dim tSC As %Status
	#Dim tException As %Exception.SystemException
	Set tSC=$$$OK
	
	Try {
		Set tSC=$$$OK, ..%HttpRequest.Location="", ..%HttpRequest.AcceptGzip=0, ..%HttpRequest.FollowRedirect=0, ..%HttpRequest.ReadRawMode=1
		Do ..%HttpRequest.Reset()
		Do ..%HttpRequest.RemoveHeader("HOST"), ..%HttpRequest.RemoveHeader("USER-AGENT"), ..%HttpRequest.RemoveHeader("REFERER")

		#; Pass along selected HTTP headers
		Set (tReq,tURL,tCT,tLen)="",tDoNotPassThrough=","_$ZCVT(..%ExcludeRequestHttpHeaders,"L")_",host,httprequest,url,version,content-length,content-type,"
		Set tHeaderKey="" For { Set tHeaderKey=pRequest.HTTPHeaders.Next(tHeaderKey)  Quit:""=tHeaderKey  Set tHeaderLwr=$ZCVT(tHeaderKey,"L")
			Do:tDoNotPassThrough'[(","_tHeaderLwr_",") ..%HttpRequest.SetHeader(tHeaderKey,pRequest.HTTPHeaders.GetAt(tHeaderKey)) ; no need to handle multiple on one line
			Set:tHeaderLwr="httprequest" tReq=pRequest.HTTPHeaders.GetAt(tHeaderKey)
			Set:tHeaderLwr="url" tURL=pRequest.HTTPHeaders.GetAt(tHeaderKey)
			Set:tHeaderLwr="host" tHost=pRequest.HTTPHeaders.GetAt(tHeaderKey)
			Set:tHeaderLwr="port" tPort=pRequest.HTTPHeaders.GetAt(tHeaderKey)
			Set:tHeaderLwr="content-type" tCT=pRequest.HTTPHeaders.GetAt(tHeaderKey)
			Set:tHeaderLwr="content-length" tLen=pRequest.HTTPHeaders.GetAt(tHeaderKey)
		}
		Set ..Adapter.HTTPServer = pRequest.Host
		Set ..Adapter.HTTPPort = pRequest.Port
		If pRequest.SSLConfig '= "" {
			Set ..Adapter.SSLConfig = pRequest.SSLConfig
		}
		
		Set tURL = pRequest.URL
		
		//Add x-rivta-original-serviceconsumer-hsaid to HTTP header
		If (pRequest.HTTPHeaders.GetAt("x-rivta-original-serviceconsumer-hsaid") = "") {
			$$$TRACE("Set HTTP header x-rivta-original-serviceconsumer-hsaid to " _ pRequest.From)
			Do ..%HttpRequest.SetHeader("x-rivta-original-serviceconsumer-hsaid",pRequest.From)
		}
		
		Set ..%HttpRequest.ContentType=tCT
		Set tSOAP12=tCT["application/soap+xml"
		Set tLen = 1
		Set tSC=..constructHTTPRequest(pRequest,tLen,tCT,tSOAP12)
		If ($$$ISERR(tSC)) { Quit }

		#; Now send to the target address
		#dim tHttpResponse As %Net.HttpResponse
		Set tSC=..Adapter.SendFormDataArray(.tHttpResponse,$S(""=tReq:"POST",1:tReq),..%HttpRequest,,,$Case(..Adapter.URL,"":tURL,"*":tURL,:..Adapter.URL))
		
		If ($$$ISERR(tSC)) {
			Set s = ""
			Do ##class(%SYSTEM.Status).DecomposeStatus(tSC, .Err)
			For i=1:1:$Get(Err) { set s = s _ "Err(" _ i _ ") = " _ Err(i) _ " / "}
		 	$$$LOGERROR("Error: " _ tHttpResponse _ " / " _ s)
		}
		
		//Ta bort SSLConfig efter anrop för att det ska fungera från icke SSL connections
		If pRequest.SSLConfig '= "" {
	   		Set ..Adapter.SSLConfig = ""
		}
		
		#; Suppress HTTP status errors and just pass back the status, headers and body
		Set:$$$ISERR(tSC)&&$$$StatusEquals(tSC,$$$EnsErrHTTPStatus) tSC=$$$OK
		
		If $IsObject(tHttpResponse.Data) {
			Set tStream=tHttpResponse.Data
		} Else {
			Set tStream=##class(%GlobalCharacterStream).%New()
			Set tSC=tStream.Write(tHttpResponse.Data)
			If ($$$ISERR(tSC)) { Quit }
		}
		#; Find starting element
		If '..%SplitResponseBody {
			Set pResponse=##class(TP.Message.SOAPResponse).%New(tStream,,tHttpResponse)
		} Else {
			Set tSC=##class(EnsLib.SOAP.GenericService).splitEnvelopeStream(tStream,.tEnvelopeStream,.tBodyStream)
			If ($$$ISERR(tSC)) { Quit }
			Set pResponse=##class(TP.Message.SOAPResponse).%New(tBodyStream,,tHttpResponse,tEnvelopeStream)
		}
		Do pResponse.HTTPHeaders.SetAt(tHttpResponse.StatusLine,"StatusLine")
	}
	Catch tException {
		Set tSC = tException.AsStatus()
	}
	
	Quit tSC
]]></Implementation>
</Method>
</Class>
</Export>
